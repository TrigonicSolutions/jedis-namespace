apply plugin: 'java'
apply plugin: 'nexus'
apply plugin: 'eclipse'

group = 'com.trigonic'
version = '0.2-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.2'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'redis.clients:jedis:2.1.0'
    compile 'com.google.guava:guava:11.0.1'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.2'
    compile 'joda-time:joda-time:2.0'

    testCompile 'junit:junit:4.10'
    testCompile 'org.easymock:easymock:3.0'

    testRuntime 'org.slf4j:slf4j-simple:1.6.1'
}

task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task packageSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives packageJavadoc
    archives packageSources
}

modifyPom {
    project {
        name 'jedis-namespace'
        packaging 'jar'
        description 'Namespace support for Jedis keys'
        url 'https://github.com/TrigonicSolutions/jedis-namespace'
        inceptionYear '2011'

        parent {
            groupId 'org.sonatype.oss'
            artifactId 'oss-parent'
            version '7'
        }
 
        scm {
            url 'scm:git://github.com/TrigonicSolutions/jedis-namespace.git'
            connection 'scm:git://github.com/TrigonicSolutions/jedis-namespace.git'
        }
 
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
 
        developers {
            developer {
                id 'akrueger'
                name 'Alan Krueger'
                email 'alan@trigonic.com'
                roles{
                    role 'Developer'
                }
                timezone '-6'
            }
        }
    }
}

task createWrapper(type: Wrapper) {
    gradleVersion = '1.2'
}

